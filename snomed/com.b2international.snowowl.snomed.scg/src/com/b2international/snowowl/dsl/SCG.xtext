grammar com.b2international.snowowl.dsl.SCG //hidden (WS, SL_COMMENT, ML_COMMENT)
generate scg "http://www.b2international.com/snowowl/SCG"
import 'http://www.eclipse.org/emf/2002/Ecore' as ecore

Expression hidden(WS, SL_COMMENT, ML_COMMENT):
        concepts+=Concept (PLUS_SIGN concepts+=Concept)* (COLON (attributes+=Attribute (COMMA attributes+=Attribute)*)? (groups+=Group)* )?;

Concept :
        id=ConceptId (PIPE WS* term=Term WS* PIPE)?;

Group :
        OPENING_CURLY_BRACKET attributes+=Attribute (COMMA attributes+=Attribute)* CLOSING_CURLY_BRACKET;

Attribute :
        name=Concept EQUAL_SIGN value=AttributeValue;

AttributeValue :
        Concept | (OPENING_ROUND_BRACKET Expression CLOSING_ROUND_BRACKET);


Term hidden() : (TermCharacter)+ (WS+ (TermCharacter)+)*; 
ConceptId hidden(): DIGIT_NONZERO (DIGIT_NONZERO|ZERO)(DIGIT_NONZERO|ZERO)(DIGIT_NONZERO|ZERO)(DIGIT_NONZERO|ZERO)(DIGIT_NONZERO|ZERO)+;
TermCharacter hidden() : DIGIT_NONZERO|ZERO|LETTER|COMMA|OPENING_CURLY_BRACKET|
	CLOSING_CURLY_BRACKET|EQUAL_SIGN|OPENING_ROUND_BRACKET|CLOSING_ROUND_BRACKET|PLUS_SIGN|COLON|OTHER_ALLOWED_TERM_CHARACTER;

terminal ZERO : '0';
terminal DIGIT_NONZERO  : '1'..'9';
terminal LETTER : ('a'..'z' | 'A'..'Z');
terminal WS : (' '|'\t'|'\n'|'\r');
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;
terminal PIPE : '|';
terminal COLON : ':';
terminal OPENING_CURLY_BRACKET : '{';
terminal CLOSING_CURLY_BRACKET: '}';
terminal EQUAL_SIGN: '=';
terminal COMMA: ',';
terminal OPENING_ROUND_BRACKET : '(';
terminal CLOSING_ROUND_BRACKET : ')';
terminal PLUS_SIGN : '+';
terminal OTHER_ALLOWED_TERM_CHARACTER : .;
//terminal OTHER_ALLOWED_TERM_CHARACTER : '-'|'/'|'\\'|';'|'.'|'?'|'_'|'<'|'>'|'['|']';
	
// ------ Original Snomed Composition Grammar (2008-12-23) ------
//expression = concept *("+" concept) [":" ws refinements ] 
//concept = ws conceptId ws ["|" ws term ws "|" ws] 
//conceptId = sctId 
//term = 1*nonwsnonpipe *( 1*SP 1*nonwsnonpipe )
//refinements = ( attributeSet *attributeGroup ) / 1*attributeGroup 
//attributeGroup = "{" attributeSet "}" ws
//attributeSet = attribute *("," attribute) 
//attribute = attributeName "=" attributeValue 
//attributeName = ws attributeNameId ws ["|" ws term ws "|" ws] 
//attributeValue = concept / (ws "(" expression ")" ws) 
//attributeNameId = sctId 
//sctId = digitNonZero 5*17( digit ) 
//ws =*(SP/HTAB/CR/LF) ;whitespace
//SP = %x20 
//HTAB = %x09 
//CR = %x0D 
//LF = %x0A 
//digit = %x30-39 
//digitNonZero = %x31-39 ; digits 1 through 9, but excluding 0 
//nonwsnonpipe = %x21-7B / %x7D-7E / UTF8-2 / UTF8-3 / UTF8-4
