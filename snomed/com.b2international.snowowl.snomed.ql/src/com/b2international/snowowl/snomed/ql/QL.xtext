/*
 * Copyright 2019 B2i Healthcare Pte Ltd, http://b2i.sg
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
grammar com.b2international.snowowl.snomed.ql.QL
with com.b2international.snowowl.snomed.ecl.Ecl

generate ql "http://www.b2international.com/snowowl/snomed/QL"

Query:
	{Query} (constraint=Constraint)?;
	
Constraint:
	Disjunction;
	
Disjunction returns Constraint:
	Conjunction ({Disjunction.left = current} 'OR' right = Conjunction)*;

Conjunction returns Constraint:
	Exclusion ({Conjunction.left = current} 'AND' right = Exclusion)*;

Exclusion returns Constraint:
	Filter ({Exclusion.left = current} 'MINUS' right = Filter)?;

NestedFilter:
  ROUND_OPEN constraint = Constraint ROUND_CLOSE 
;

Filter:
	ActiveFilter | EclFilter | Description | NestedFilter
;

EclFilter:
	ECL (ecl = Script)
;

ActiveFilter:
	ACTIVE (active = Boolean)
;

Description:
	{Description} OPEN_DOUBLE_BRACES filter = DescriptionFilter CLOSE_DOUBLE_BRACES
;

DescriptionFilter:
	{DescriptionFilter} ((termFilter = TermFilter)? 
		& (active=ActiveTerm)?
		& (type=Descriptiontype)?
		& (regex = RegularExpression)?
	)
;

TermFilter:
	TERM EQUAL (term = STRING)
;

RegularExpression:
	REGEX EQUAL (regex = STRING)
;

Descriptiontype:
	DESCRIPTION_TYPE EQUAL (ecl = Script)
;

ActiveTerm:
	ACTIVE EQUAL (active = Boolean)
;

Boolean:
	TRUE | FALSE;

terminal TERM:
	'term'
;

terminal ECL:
	'ecl'
;

terminal ACTIVE:
	'active'
;

terminal TRUE:
	'true'
;

terminal FALSE:
	'false'
;

terminal REGEX:
	'regex'
;

terminal DESCRIPTION_TYPE:
	'type'
;

terminal OPEN_DOUBLE_BRACES:
	'{{'
;

terminal CLOSE_DOUBLE_BRACES:
	'}}'
;
